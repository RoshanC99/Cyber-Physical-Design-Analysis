package Crazyflie_Software
public
  with Crazyflie_Types;

  process nRF51822_Firmware
  features
    Syslink_Packet_TX: out data port Crazyflie_Types::Syslink_Packet;
    Syslink_Packet_RX: in data port Crazyflie_Types::Syslink_Packet;
  flows
    f1 : flow source syslink_Packet_TX{ latency => 1 ms .. 2 ms; };

  end nRF51822_Firmware;

  process STM32F405_Firmware
  features
    Syslink_Packet_Rx : in data port Crazyflie_Types::Syslink_Packet;
    Syslink_Packet_Tx : out data port Crazyflie_Types::Syslink_Packet;
    DOFs           : in data port Crazyflie_Types::Six_Axis.impl;
    
    -- Roshan Cheriyan begin q2 addition
    Magneto_Data : in data port Crazyflie_Types::Six_Axis.impl;
    -- Roshan Cheriyan end q2 addition 

    Rate_1         : out data port Crazyflie_Types::PWM_Rate;
    Rate_2         : out data port Crazyflie_Types::PWM_Rate;
    Rate_3         : out data port Crazyflie_Types::PWM_Rate;
    Rate_4         : out data port Crazyflie_Types::PWM_Rate;
    
    -- Roshan Cheriyan begin q8 addition
    Flow_Data_In   : in data port Crazyflie_Types::Flow_Data.impl;
    -- Roshan Cheriyan end q8 addition 

  flows
    f1 : flow path Syslink_Packet_Rx -> Rate_1;
    f2 : flow path DOFs              -> Rate_1;
    
    -- Roshan Cheriyan begin q2 addition
    f3 : flow path DOFs -> Rate_2;
    f4 : flow path DOFs -> Rate_3;
    f5 : flow path DOFs -> Rate_4;
    
    f6 : flow path Syslink_Packet_Rx -> Rate_2;
    f7 : flow path Syslink_Packet_Rx -> Rate_3;
    f8 : flow path Syslink_Packet_Rx -> Rate_4;
    
    f9 : flow path Magneto_Data -> Rate_1;     
    -- Roshan Cheriyqn end q2 addition
    
    -- Roshan Cheriyan begin q8 addition
    f10 : flow path Flow_Data_In -> Rate_1;
    -- Roshan Cheriyan end q8 addition        
    
    
    
  annex EMV2 {**
    use types Crazyflie_Errors;
    use behavior Crazyflie_Errors::simple;

    error propagations
      Rate_1 : out propagation{ValueErroneous};
      DOFs: in propagation{ValueErroneous};
      Syslink_Packet_RX: in propagation{ValueErroneous};
      processor : in propagation{Lost, ValueErroneous};

   flows
     f1 : error path Syslink_Packet_RX -> Rate_1;
     f2 : error path DOFs{ValueErroneous} -> Rate_1{ValueErroneous};
     f3 : error path processor{lost, ValueErroneous} -> Rate_1{ValueErroneous};
    end propagations;

    component error behavior
     transitions
       t1 : Operational -[ DOFs{ValueErroneous} or
       	               --    processor{ValueErroneous} or
       	                   processor{Lost}
       ]-> Failed;
     propagations
       Failed -[]-> Rate_1{ValueErroneous}; -- XXX
     end component;

  **};
  end STM32F405_Firmware;

  process implementation STM32F405_Firmware.impl
  subcomponents
    CRTP_Tx_Task     : thread CRTP_Tx_Task;
    CRTP_Rx_Task     : thread CRTP_Rx_Task;
    Power_Management : thread Power_Management;
    Main_Loop        : thread Main_Loop;

  connections
    C1 : port CRTP_Tx_Task.Msg -> Syslink_Packet_Tx;
    C8 : port Main_Loop.Syslink_Packet_Tx -> CRTP_Tx_Task.SysLink_Packet;

    C2 : port Syslink_Packet_Rx -> CRTP_Rx_Task.Msg;
    C7 : port CRTP_Rx_Task.SysLink_Packet -> Main_Loop.Syslink_Packet_Rx;

    C3 : port Main_Loop.Rate_1 -> Rate_1;
    C4 : port Main_Loop.Rate_2 -> Rate_2;
    C5 : port Main_Loop.Rate_3 -> Rate_3;
    C6 : port Main_Loop.Rate_4 -> Rate_4;

    C9 : port DOFs -> Main_Loop.DOFs;
    
    -- Roshan Cheriyan begin q2 addition
    C10c : port Magneto_Data -> Main_Loop.Magneto_Data;
    -- Roshan Cheriyan end q2 addition
    
    -- Roshan Cheriyan begin q8 addition
    C10fd : port Flow_Data_In -> Main_Loop.Flow_Data_In;
    -- Roshan Cheriyan end q8 addition 

  flows
    f1 : flow path Syslink_Packet_Rx -> C2 -> CRTP_Rx_Task.fs1 -> C7
      -> Main_Loop.fs1 -> C3 -> Rate_1;
    f2 : flow path DOFs -> C9 -> Main_Loop.fs2 -> C3 -> Rate_1;
    
    
    -- Roshan Cheriyan begin q2 addition
    f3 : flow path DOFs -> C9 -> Main_Loop.fs3 -> C4 -> Rate_2;
    f4 : flow path DOFs -> C9 -> Main_Loop.fs4 -> C5 -> Rate_3;
    f5 : flow path DOFs -> C9 -> Main_Loop.fs5 -> C6 -> Rate_4;
    
    f6 : flow path Syslink_Packet_Rx -> C2 -> CRTP_Rx_Task.fs1 -> C7
     -> Main_Loop.fs6 -> C4 -> Rate_2;
    f7 : flow path Syslink_Packet_Rx -> C2 -> CRTP_Rx_Task.fs1 -> C7
     -> Main_Loop.fs7 -> C5 -> Rate_3;
    f8 : flow path Syslink_Packet_Rx -> C2 -> CRTP_Rx_Task.fs1 -> C7
     -> Main_Loop.fs8 -> C6 -> Rate_4;
     
    f9 : flow path Magneto_Data -> C10c -> Main_Loop.fs9 -> C3 -> Rate_1;
    -- Roshan Cheriyan end q2 addition
    
    -- Roshan Cheriyan begin q8 addition
    f10 : flow path Flow_Data_In -> C10fd -> Main_Loop.fs10 -> C3 -> Rate_1;
    -- Roshan Cheriyan end q8 addition  
           
    
    
  end STM32F405_Firmware.impl;

  thread CRTP_Tx_Task
  features
    Msg : out data port Crazyflie_Types::Syslink_Packet;
    Syslink_Packet : in data port Crazyflie_Types::Syslink_Packet;
  properties
    Priority => 2;
    Dispatch_Protocol => Sporadic;
    Period => 1000 us;
    Compute_Execution_Time => 10 us .. 50 us;
  end CRTP_Tx_Task;

  thread CRTP_Rx_Task
  features
    Msg : in data port Crazyflie_Types::Syslink_Packet;
    Syslink_Packet : out data port Crazyflie_Types::Syslink_Packet;
  flows
    fs1 : flow path Msg -> Syslink_Packet;
  properties
    Priority => 2;
    Dispatch_Protocol => Sporadic;
    Period => 1000 us;
    Compute_Execution_Time => 10 us .. 50 us;
  end CRTP_Rx_Task;

  thread Power_Management
  properties
    Priority => 2;
    Dispatch_Protocol => Periodic;
    Period => 500 us;
    Compute_Execution_Time => 10 us .. 20 us;
  end Power_Management;

  thread Main_Loop
  features
    DOFs              : in data port Crazyflie_Types::Six_Axis.impl;

    Syslink_Packet_Rx : in data port Crazyflie_Types::Syslink_Packet;
    Syslink_Packet_Tx : out data port Crazyflie_Types::Syslink_Packet;

    Rate_1            : out data port Crazyflie_Types::PWM_Rate;
    Rate_2            : out data port Crazyflie_Types::PWM_Rate;
    Rate_3            : out data port Crazyflie_Types::PWM_Rate;
    Rate_4            : out data port Crazyflie_Types::PWM_Rate;
    
    
    -- Roshan Cheriyan begin q2 addition
    Magneto_Data : in data port Crazyflie_Types::Six_Axis.impl;
    -- Roshan Cheriyan end q2 addition 
    
    -- Roshan Cheriyan begin q8 addition
    Flow_Data_In      : in data port Crazyflie_Types::Flow_Data.impl;
    -- Roshan Cheriyan end q8 addition 

  flows
    fs1 : flow path Syslink_Packet_Rx -> Rate_1;
    fs2 : flow path DOFs              -> Rate_1;
    
    -- Roshan Cheriyan begin q2 addition
    fs3 : flow path DOFs -> Rate_2;
    fs4 : flow path DOFs -> Rate_3;
    fs5 : flow path DOFs -> Rate_4;
    
    fs6 : flow path Syslink_Packet_Rx -> Rate_2;
    fs7 : flow path Syslink_Packet_Rx -> Rate_3;
    fs8 : flow path Syslink_Packet_Rx -> Rate_4;
    
    fs9 : flow path Magneto_Data -> Rate_1;
    -- Roshan Cheriyan end q2 addition 
    
    -- Roshan Cheriyan begin q8 addition 
    fs10 : flow path Flow_Data_In -> Rate_1;
    -- Roshan Cheriyan end q8 addition 
        

  properties
    Priority => 3;
    Dispatch_Protocol => Periodic;
    Period => 2000 us;
    Compute_Execution_Time => 100 us .. 200 us;
  end Main_Loop;

end Crazyflie_Software;